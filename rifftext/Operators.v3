// Copyright 2025 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

component Operators {
	// Transpose the steps {delta} semitones.
	def transposeMelodySteps(s: Array<MelodyStep>, delta: i16) -> Array<MelodyStep> {
		return Arrays.map(s, transposeMelodyStep(_, delta));
	}
	// Transpose a step by {delta} semitones.
	def transposeMelodyStep(s: MelodyStep, delta: i16) -> MelodyStep {
		match (s) {
			Voice(x) => return MelodyStep.Voice(i16.view(x + delta));
			Chord(x) => return MelodyStep.Chord(Arrays.map(x, i16.+(delta, _)));
			Rest => return MelodyStep.Rest;
		}
	}
	// Transpose a melody by {delta} semitones by simply adjusting the tonic.
	def transposeMelody(m: Melody, delta: i16) -> Melody {
		return Melody(m.tonic.plus(delta, NoteDir.NATURAL), m.steps);
	}
	// Convert a melody to pure rhythm by replacing all voices with 0.
	def setMelodyVoicesToZero(m: Melody) -> Melody {
		var steps = Arrays.map(m.steps, fun (step: MelodyStep, d: Duration) -> (MelodyStep, Duration) {
			match (step) {
				Voice(x) => return (MelodyStep.Voice(0), d);
				Chord(x) => return (MelodyStep.Voice(0), d);
				Rest => return (MelodyStep.Rest, d);
			}
		});
		return Melody(m.tonic, steps);
	}
}