// Copyright 2025 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

component Operators {
	// Transpose the steps {delta} semitones.
	def transposeSteps(s: Array<Step>, delta: i16) -> Array<Step> {
		return Arrays.map(s, transposeStep(_, delta));
	}
	// Transpose a step by {delta} semitones.
	def transposeStep(s: Step, delta: i16) -> Step {
		match (s) {
			Voice(x) => return Step.Voice(i16.view(x + delta));
			Chord(x) => return Step.Chord(Arrays.map(x, i16.+(delta, _)));
			Rest => return Step.Rest;
		}
	}
	// Transpose a melody by {delta} semitones by simply adjusting the tonic.
	def transposeMelody(m: Melody, delta: i16) -> Melody {
		return Melody(m.tonic.plus(delta, NoteDir.NATURAL), m.steps);
	}
	// Convert a melody to pure rhythm by replacing all voices with 0.
	def setMelodyVoicesToZero(m: Melody) -> Melody {
		var steps = Arrays.map(m.steps, fun (step: Step, d: Duration) -> (Step, Duration) {
			match (step) {
				Voice(x) => return (Step.Voice(0), d);
				Chord(x) => return (Step.Voice(0), d);
				Rest => return (Step.Rest, d);
			}
		});
		return Melody(m.tonic, steps);
	}
}